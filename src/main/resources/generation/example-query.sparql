PREFIX cims: <http://iec.ch/TC57/1999/rdf-schema-extensions-19990926#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX cim: <http://iec.ch/TC57/2013/CIM-schema-cim16#>
PREFIX uml: <http://iec.ch/TC57/NonStandard/UML#>

SELECT ?s ?kind (COUNT(?planningValue) as ?count) (GROUP_CONCAT(?planningValueName;SEPARATOR=", ") as ?offenders)
WHERE {
    ?s  a cim:RedispatchableResource ;
        cim:RedispatchableResource.controllableResource?/^cim:PlanningValue.powerSystemResource ?planningValue .
    ?planningValue cim:PlanningValue.kind ?kind ;
                   cim:IdentifiedObject.name ?planningValueName .
}
GROUP BY ?kind ?s
HAVING(?count > 1)